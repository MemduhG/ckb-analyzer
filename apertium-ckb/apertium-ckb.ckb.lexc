! Morphological Transducer for Sorani Kurdish

Multichar_Symbols

! Part of speech categories
%<n%>     ! Noun
%<adj%>   ! Adjective
%<v%>     ! Verb

! Verbal stuff
%<tv%>     ! Transitive  Verb
%<iv%>     ! 
%<pri%>     ! 
%<sub%>     ! 
%<neg%>     ! 
%<past%>     ! 

! Number morphology
%<pl%>  ! Plural
%<sg%>  ! Singular

! Definiteness
%<abs%>  ! Absolute
%<def%>  ! Indefinite
%<ind%>  ! Definite

%<det%>  ! Determiner

! Ezafet
%<ezl%>  ! Loose ezafet
%<ezc%>  ! Close ezafet

%<pr%>   ! Preposition
%<post%> ! Postposition
%<cnjcoo%> ! Coordinating Conjunction

! Other symbols
%>      ! Morpheme boundary

! 2L stuff

%{VSUF%}
%[%+prog%]
%[%+subj%]
%[%-prog%]
%[%-subj%]
%+

%[%+neg%]
%[%-neg%]

! Flags
@P.Asp.Prog@
@R.Asp.Prog@

@P.Mood.Subj@
@R.Mood.Subj@
@P.POL.NEG@
@R.POL.NEG@

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!                       M O R P H O T A C T I C S                         !!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Root

ConjRoot ;
PrRoot ;
NounRoot ;
AdjRoot ;
Pronouns ; 
AdvRoot ;
Dets ;
! Verb ;
Cop ;
Haver ;
NPs ;
Numbers ;
Prefix ;
Verb_Past ;
Hatin ;


LEXICON Hatin

hatin%<v%>%<iv%>%<inf%>:hatin # ;
hatin%<v%>%<iv%>%<pri%>%<p1%>%<sg%>:dem # ;
hatin%<v%>%<iv%>%<pri%>%<p2%>%<sg%>:dei # ;
hatin%<v%>%<iv%>%<pri%>%<p2%>%<sg%>:deit # ;
hatin%<v%>%<iv%>%<pri%>%<p1%>%<pl%>:dein # ;
hatin%<v%>%<iv%>%<pri%>%<p2%>%<pl%>:den # ;
hatin%<v%>%<iv%>%<pri%>%<p2%>%<pl%>:den # ;
hatin%<v%>%<iv%>%<pri%>%<p1%>%<sg%>:iem # ;
hatin%<v%>%<iv%>%<pri%>%<p2%>%<sg%>:iei # ;
hatin%<v%>%<iv%>%<pri%>%<p2%>%<sg%>:ieit # ;
hatin%<v%>%<iv%>%<pri%>%<p1%>%<pl%>:iein # ;
hatin%<v%>%<iv%>%<pri%>%<p2%>%<pl%>:ien # ;
hatin%<v%>%<iv%>%<pri%>%<p2%>%<pl%>:ien # ;
hatin%<v%>%<iv%>%<pri%>%<neg%>%<p1%>%<sg%>:naiem # ;
hatin%<v%>%<iv%>%<pri%>%<neg%>%<p2%>%<sg%>:naiei # ;
hatin%<v%>%<iv%>%<pri%>%<neg%>%<p2%>%<sg%>:naieit # ;
hatin%<v%>%<iv%>%<pri%>%<neg%>%<p3%>%<sg%>:naie # ;
hatin%<v%>%<iv%>%<pri%>%<neg%>%<p3%>%<sg%>:naiet # ;
hatin%<v%>%<iv%>%<pri%>%<neg%>%<p1%>%<pl%>:naiein # ;
hatin%<v%>%<iv%>%<pri%>%<neg%>%<p2%>%<pl%>:naien # ;
hatin%<v%>%<iv%>%<pri%>%<neg%>%<p3%>%<pl%>:naien # ;

LEXICON Cop_Voc

%+bwwn%<vbcop%>%<pri%>%<p1%>%<sg%>:m # ;
%+bwwn%<vbcop%>%<pri%>%<p2%>%<sg%>:i # ;
%+bwwn%<vbcop%>%<pri%>%<p2%>%<sg%>:it # ;
%+bwwn%<vbcop%>%<pri%>%<p3%>%<sg%>:ie # ;
%+bwwn%<vbcop%>%<pri%>%<p1%>%<pl%>:in # ;
%+bwwn%<vbcop%>%<pri%>%<p2%>%<pl%>:n # ;
%+bwwn%<vbcop%>%<pri%>%<p3%>%<pl%>:n # ;

LEXICON Cop_Cons

%+bwwn%<vbcop%>%<pri%>%<p1%>%<sg%>:m # ;
%+bwwn%<vbcop%>%<pri%>%<p2%>%<sg%>:i # ;
%+bwwn%<vbcop%>%<pri%>%<p2%>%<sg%>:it # ;
%+bwwn%<vbcop%>%<pri%>%<p3%>%<sg%>:e # ;
%+bwwn%<vbcop%>%<pri%>%<p1%>%<pl%>:in # ;
%+bwwn%<vbcop%>%<pri%>%<p2%>%<pl%>:n # ;
%+bwwn%<vbcop%>%<pri%>%<p3%>%<pl%>:n # ;

LEXICON Cop

bwwn%<vbcop%>%<past%>%<p1%>%<sg%>:bwwm # ;
bwwn%<vbcop%>%<past%>%<p3%>%<sg%>:bww # ;
bwwn%<vbcop%>%<past%>%<p2%>%<pl%>:bwwn # ;
bwwn%<vbcop%>%<past%>%<p3%>%<pl%>:bwwn # ;
bwwn%<vbcop%>%<inf%>:bwwn # ;
bwwn%<vbcop%>%<past%>%<neg%>%<p3%>%<sg%>:nebww # ;
bwwn%<vbcop%>%<perf%>%<p3%>%<sg%>:bwwe # ;
bwwn%<vbcop%>%<perf%>%<neg%>%<p3%>%<sg%>:nebwwe # ;
bwwn%<vbcop%>%<neg%>%<pri%>%<p3%>%<sg%>:nie # ;

LEXICON Haver

hebwwn%<vbhaver%>%<pri%>%<p3%>%<sg%>:heie # ;
hebwwn%<vbhaver%>%<past%>%<p3%>%<sg%>:hebww # ;
hebwwn%<vbhaver%>%<past%>%<neg%>%<p3%>%<sg%>:henebww # ;

LEXICON Num 
%<num%>: # ;

LEXICON Ord

%<num%>%<ord%>: # ;

LEXICON Numbers

iek:iek Num ;
iekem:iekem Ord ;
dww:dww Num ;
sê:sê Num ;

pênc:pênc Num ;


!LEXICON Clitics 
!%<sim%>:ş # ; 

LEXICON Pronouns

mn%<prn%>%<prs%>%<p1%>%<sg%>:mn # ;
to%<prn%>%<prs%>%<p2%>%<sg%>:to # ;
yew%<prn%>%<prs%>%<p3%>%<sg%>:yew # ;
yême%<prn%>%<prs%>%<p3%>%<sg%>:yême # ;
yêwe%<prn%>%<prs%>%<p3%>%<sg%>:yêwe # ;
yewan%<prn%>%<prs%>%<p3%>%<sg%>:yewan # ;


!LEXICON DET-DEM
!%<det%>%<dem%>: # ;

LEXICON DET-QNT
%<det%>%<qnt%>: # ;

LEXICON n__derga

%<n%>%<sg%>%<abs%>: # ;
%<n%>%<sg%>%<abs%>: Cop_Voc ;
%<n%>%<sg%>%<def%>:%>ke # ;
%<n%>%<sg%>%<def%>:%>ke Cop_Voc ;
%<n%>%<sg%>%<ind%>:%>iêk # ;
%<n%>%<sg%>%<ind%>:%>iêk Cop_Cons ;
%<n%>%<sg%>%<dem%>:%>ie # ;
%<n%>%<sg%>%<dem%>:%>ie Cop_Voc ;
%<n%>%<pl%>%<ind%>:%>n # ;
%<n%>%<pl%>%<ind%>:%>n Cop_Cons ;
%<n%>%<pl%>%<def%>:%>kan # ;
%<n%>%<pl%>%<def%>:%>kan Cop_Cons ;

%<n%>%<sg%>%<abs%>%<ezl%>:%>i # ;
%<n%>%<sg%>%<def%>%<ezl%>:%>kei # ;
%<n%>%<sg%>%<ind%>%<ezl%>:%>iêki # ;
%<n%>%<pl%>%<ind%>%<ezl%>:%>ni # ;
%<n%>%<pl%>%<def%>%<ezl%>:%>kani # ;

%<n%>%<sg%>%<abs%>%<ezc%>:%>ie # ;


LEXICON n__ktaw

%<n%>%<sg%>%<abs%>: # ;
%<n%>%<sg%>%<abs%>: Cop_Cons ;
%<n%>%<sg%>%<def%>:%>eke # ;
%<n%>%<sg%>%<def%>:%>eke Cop_Voc ;
%<n%>%<sg%>%<ind%>:%>êk # ;
%<n%>%<sg%>%<ind%>:%>êk Cop_Cons ;
%<n%>%<sg%>%<dem%>:%>e # ;
%<n%>%<sg%>%<dem%>:%>e Cop_Voc ;
%<n%>%<pl%>%<ind%>:%>an # ;
%<n%>%<pl%>%<ind%>:%>an Cop_Cons ;
%<n%>%<pl%>%<def%>:%>ekan # ;
%<n%>%<pl%>%<def%>:%>ekan Cop_Cons ;

%<n%>%<sg%>%<abs%>%<ezl%>:%>i # ;
%<n%>%<sg%>%<def%>%<ezl%>:%>ekei # ;
%<n%>%<sg%>%<ind%>%<ezl%>:%>êki # ;
%<n%>%<pl%>%<ind%>%<ezl%>:%>ani # ;
%<n%>%<pl%>%<def%>%<ezl%>:%>ekani # ;

%<n%>%<sg%>%<abs%>%<ezc%>:%>e # ;
%<n%>%<pl%>%<abs%>%<ezc%>:%>ane # ;

LEXICON n__nam_e

%<n%>%<sg%>%<abs%>:%>e # ;
%<n%>%<sg%>%<abs%>:%>e Cop_Voc ;
%<n%>%<sg%>%<def%>:%>eke # ;
%<n%>%<sg%>%<def%>:%>eke Cop_Voc ;
%<n%>%<sg%>%<ind%>:%>eiek # ;
%<n%>%<sg%>%<ind%>:%>eiek Cop_Cons ;
%<n%>%<sg%>%<dem%>:%>eie # ;
%<n%>%<sg%>%<dem%>:%>eie Cop_Voc ;
%<n%>%<pl%>%<ind%>:%>an # ;
%<n%>%<pl%>%<ind%>:%>an Cop_Cons ;
%<n%>%<pl%>%<def%>:%>ekan # ;
%<n%>%<pl%>%<def%>:%>ekan Cop_Cons ;

%<n%>%<sg%>%<abs%>%<ezl%>:%>ei # ;
%<n%>%<sg%>%<def%>%<ezl%>:%>ekei # ;
%<n%>%<sg%>%<ind%>%<ezl%>:%>eieki # ;
%<n%>%<pl%>%<ind%>%<ezl%>:%>ani # ;
%<n%>%<pl%>%<def%>%<ezl%>:%>ekani # ;

%<n%>%<sg%>%<abs%>%<ezc%>:%>e # ;
%<n%>%<pl%>%<abs%>%<ezc%>:%>ane # ;


LEXICON adj__drej

%<adj%>: # ;
%<adj%>%<cmp%>:%>tr # ;
%<adj%>%<sup%>:%>trin # ;
%<adj%>%<dem%>:%>e # ;
%<adj%>%<sg%>%<def%>:%>eke # ;
%<adj%>%<ind%>:%>êk # ;
%<adj%>%<pl%>%<ind%>:%>an # ;
%<adj%>%<pl%>%<dem%>:%>ane # ;
%<adj%>%<pl%>%<def%>:%>ekan # ;


LEXICON adj__gewre

%<adj%>:e # ;
%<adj%>%<cmp%>:e%>tr # ;
%<adj%>%<sup%>:e%>trin # ;
%<adj%>%<dem%>:e # ;
%<adj%>%<sg%>%<def%>:e%>ke # ;
%<adj%>%<ind%>:e%>iek # ;
%<adj%>%<pl%>%<ind%>:%>an # ;
%<adj%>%<pl%>%<dem%>:%>ane # ;
%<adj%>%<pl%>%<def%>:%>ekan # ;


LEXICON CC
%<cnjcoo%>: # ;

LEXICON CS
%<cnjsub%>: # ;

LEXICON Pr
%<pr%>: # ;

LEXICON Adv
%<adv%>: # ;

LEXICON cons_past
%<p1%>%<sg%>:%>m # ;
%<p2%>%<sg%>:%>i # ;
%<p2%>%<sg%>:%>it # ;
%<p3%>%<sg%>: # ;
%<p1%>%<pl%>:%>in # ;
%<p2%>%<pl%>:%>n # ;
%<p3%>%<pl%>:%>n # ;

LEXICON past__krd 

%<past%>: cons_past ;
%<inf%>:%>n # ;
%<perf%>:wwe # ;

LEXICON pres__k

%<p1%>%<sg%>:%>em # ;
%<p2%>%<sg%>:%>ei # ;
%<p2%>%<sg%>:%>eit # ;
%<p3%>%<sg%>:%>a # ;
%<p3%>%<sg%>:%>at # ;
%<p1%>%<pl%>:%>ein # ;
%<p2%>%<pl%>:%>en # ;
%<p3%>%<pl%>:%>en # ;

LEXICON pres__ç

%<p1%>%<sg%>:%>m # ;
%<p2%>%<sg%>:%>i # ;
%<p2%>%<sg%>:%>it # ;
%<p3%>%<sg%>:%>ê # ;
%<p3%>%<sg%>:%>êt # ;
%<p1%>%<pl%>:%>in # ;
%<p2%>%<pl%>:%>n # ;
%<p3%>%<pl%>:%>n # ;

LEXICON pres__r_o

%<p1%>%<sg%>:%>om # ;
%<p2%>%<sg%>:%>oit # ;
! %<p2%>%<sg%>:%>oi # ;
%<p3%>%<sg%>:%>wa # ;
%<p3%>%<sg%>:%>wat # ;
%<p1%>%<pl%>:%>oin # ;
%<p2%>%<pl%>:%>on # ;
%<p3%>%<pl%>:%>on # ;

LEXICON pres__ger

%<p1%>%<sg%>:%>êm # ;
%<p2%>%<sg%>:%>êi # ;
%<p2%>%<sg%>:%>êit # ;
%<p3%>%<sg%>:%>ê # ;
%<p3%>%<sg%>:%>êt # ;
%<p1%>%<pl%>:%>êin # ;
%<p2%>%<pl%>:%>ên # ;
%<p3%>%<pl%>:%>ên # ;


LEXICON tv__krd 

%<v%>%<tv%>: past__krd ;

LEXICON iv__ç

%<v%>%<iv%>%<pri%>%[%-prog%]: pres__ç ;
%<v%>%<iv%>%<sub%>%[%-subj%]: pres__ç ;
%<v%>%<iv%>%<neg%>%<pri%>%[%-neg%][%-prog%]: pres__ç ;
%<v%>%<iv%>%<neg%>%<sub%>%[%-neg%][%-subj%]: pres__ç ;

LEXICON tv__nwws

%<v%>%<tv%>%<pri%>%[%-prog%]: pres__ç ;
%<v%>%<tv%>%<sub%>%[%-subj%]: pres__ç ;
%<v%>%<tv%>%<neg%>%<pri%>%[%-neg%][%-prog%]: pres__ç ;
%<v%>%<tv%>%<neg%>%<sub%>%[%-neg%][%-subj%]: pres__ç ;

LEXICON iv__g

%<v%>%<iv%>%<pri%>%[%-prog%]: pres__k ;
%<v%>%<iv%>%<sub%>%[%-subj%]: pres__k ;
%<v%>%<iv%>%<neg%>%<pri%>: pres__k ;
%<v%>%<iv%>%<neg%>%<sub%>: pres__k ;


LEXICON iv__ger_e 

%<v%>%<iv%>%<pri%>%[%-prog%]: pres__ger ;
%<v%>%<iv%>%<sub%>%[%-subj%]: pres__ger ;
%<v%>%<iv%>%<neg%>%<pri%>: pres__ger ;
%<v%>%<iv%>%<neg%>%<sub%>: pres__ger ;

LEXICON iv__r_o 

%<v%>%<iv%>%<pri%>%[%-prog%]: pres__r_o ;
%<v%>%<iv%>%<sub%>%[%-subj%]: pres__r_o ;
%<v%>%<iv%>%<neg%>%<pri%>: pres__r_o ;
%<v%>%<iv%>%<neg%>%<sub%>: pres__r_o ;

LEXICON tv__x_o 

%<v%>%<tv%>%<pri%>%[%-prog%]: pres__r_o ;
%<v%>%<tv%>%<sub%>%[%-subj%]: pres__r_o ;
%<v%>%<tv%>%<neg%>%<pri%>: pres__r_o ;
%<v%>%<tv%>%<neg%>%<sub%>: pres__r_o ;



LEXICON tv__k 

%<v%>%<tv%>%<pri%>%[%-prog%]: pres__k ;
%<v%>%<tv%>%<sub%>%[%-subj%]: pres__k ;
%<v%>%<tv%>%<neg%>%<pri%>: pres__k ;
%<v%>%<tv%>%<neg%>%<sub%>: pres__k ;

LEXICON NP_pakstan
%<np%>%<top%>%<sg%>%<abs%>: # ;
!%<n%>%<sg%>%<def%>:%>eke # ;
!%<n%>%<sg%>%<ind%>:%>êk # ;
%<np%>%<top%>%<sg%>%<dem%>:%>e # ;
%<np%>%<top%>%<pl%>%<ind%>:%>an # ;
!%<n%>%<pl%>%<def%>:%>ekan # ;

%<np%>%<top%>%<sg%>%<abs%>%<ezl%>:%>i # ;
!%<n%>%<sg%>%<def%>%<ezl%>:%>ekei # ;
!%<n%>%<sg%>%<ind%>%<ezl%>:%>êki # ;
%<np%>%<top%>%<pl%>%<ind%>%<ezl%>:%>ani # ;
!%<n%>%<pl%>%<def%>%<ezl%>:%>ekani # ;

%<np%>%<top%>%<sg%>%<abs%>%<ezc%>:%>e # ;
%<np%>%<top%>%<pl%>%<abs%>%<ezc%>:%>ane # ;



!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!                          L E X I C O N                                  !!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Dets

yem%<det%>%<dem%>%<p3%>%<sg%>:yem # ;
her:her DET-QNT ;
çend:çend DET-QNT ; 
hemww:hemww DET-QNT ;

LEXICON ConjRoot

w:w CC ;
we:we CC ;
ia:ia CC ;
ian:ian CC ;
bełam:bełam CC ;
ke:ke CS ;
ta:ta CS ;
ke:kh CS ; !typo

LEXICON PrRoot

ta:ta Pr ;
le:le Pr ;
bê:bê Pr ;
behoi:behoi Pr ;
lemeř:lemeř Pr ;
leber:leber Pr ;
wek:wek Pr ;
leser:leser Pr ;
be:be Pr ;
bo:bo Pr ;
legeł:legeł Pr ;
ser:ser Pr ;

LEXICON NounRoot

ktaw:ktaw n__ktaw ;
gwnd:gwnd n__ktaw ;
dest:dest n__ktaw ;
kar:kar n__ktaw ;
jian:jian n__ktaw ;
nêwan:nêwan  n__ktaw ;
brin:brin n__ktaw ;
piaw:piaw n__ktaw ;
hotêl:hotêl n__ktaw ;
kwř:kwř n__ktaw ;
włat:włat n__ktaw ;
naw:naw n__ktaw ;
seġat:seġat n__ktaw ;
yaşpez:yaşpez n__ktaw ;
zor:zor n__ktaw ;
şar:şar n__ktaw ;
daniştwwan:daniştwwan n__ktaw ;
yencwmen:yencwmen n__ktaw ;
ser:ser n__ktaw ;
iekgrtww:iekgrtww  n__ktaw ;
zman:zman  n__ktaw ;
xełk:xełk  n__ktaw ;
yasaiş:yasaiş n__ktaw ;
şarstan:şarstan  n__ktaw ;
sał:sał   n__ktaw ;

dê:dê n__ktaw ;
pê:pê n__ktaw ;
name:nam n__nam_e ;
sede:sed n__nam_e ;
kobwwnewe:kobwwnew n__nam_e ;
jmare:jmar n__nam_e ;
pare:par n__nam_e ;
nawçe:nawç n__nam_e ;
çayxane:çayxan n__nam_e ;
netewe:netew n__nam_e ;
laşe:laş n__nam_e ;
bełge:bełg n__nam_e ;
zorbe:zorb n__nam_e ;
břiarname:břiarnam n__nam_e ;

derga:derga n__derga ;
parêzga:parêzga n__derga ;
dwa:dwa n__derga ;

LEXICON AdjRoot

gewre:gewr adj__gewre ;
drêj:drêj adj__drej ;
drwst:drwst adj__drej ;
dj:dj adj__drej ;
sewz:sewz adj__drej ; 
zeḣmet:zeḣmet adj__drej ;
yekgrtww:yekgrtww adj__drej ;

LEXICON AdvRoot

herweha:herweha Adv ;
zor:zor Adv ;
her:her Adv ;

LEXICON Verb_Past

krdn:krd tv__krd ;

LEXICON Verb_Pres 

çwwn:ç iv__ç ;
nwwsin:nwws tv__nwws ;
krdn:k tv__k ;
xstn:x tv__k ;
brdn:b tv__k ;
dan:d tv__k ;
geiştn:g iv__g ;

geřan:geř iv__ger_e ;
roiştn:r iv__r_o ;
xwardn:x tv__x_o ;

LEXICON Prefix

%[%+bare%]: Verb_Pres ;
%[%+subj%]:b Verb_Pres ; 
%[%+subj%]: Verb_Pres ; 
%[%+prog%]:de Verb_Pres ;
%[%+prog%]:ye Verb_Pres ;
%[%+prog%]%[%+neg%]:nede Verb_Pres ;
%[%+prog%]%[%+neg%]:na Verb_Pres ;
%[%+subj%]%[%+neg%]:ne Verb_Pres ;

LEXICON NPs

kwrdstan:kwrdstan NP_pakstan ;
pakstan:pakstan NP_pakstan ;
yêran:yêran NP_pakstan ;

! Resources:
! http://wiki.apertium.org/wiki/Starting_a_new_language_with_HFST#Lexicon
! http://wiki.apertium.org/wiki/Lttoolbox_and_lexc
! http://wiki.apertium.org/wiki/Apertium-specific_conventions_for_lexc
! https://kitwiki.csc.fi/twiki/bin/view/KitWiki/HfstHome
! https://github.com/hfst/
! http://www.cis.upenn.edu/~cis639/docs/lexc.html
! http://wiki.apertium.org/wiki/Hfst

